#!/usr/bin/env zsh

mysql-drop-database() {
    local -r progname="${0##*/}"
    local help

    zmodload zsh/zutil
    zparseopts -D -F -K -- {h,-help}=help || return 1

    if [[ $# -eq 0 ]] || [[ -n "${help}" ]]; then
        echo -n "
Usage: ${progname} <database>

Example:

  ${progname} mydb
"
        return 0
    fi

    local -r database="${1}"

    [[ "${database}" =~ '^(sys|mysql|information_schema|performance_schema)$' ]] && {
        echo "Can't drop system database: ${database}"
        return 1
    } >&2

    command mysql -e "DROP DATABASE \`${database}\`"
}

mysql-drop-database "$@"
