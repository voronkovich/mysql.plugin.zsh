#!/usr/bin/env zsh

mysql-create-user-and-database() {
    local -r progname="${0##*/}"
    local help
    local -a charset
    local -a collate

    zmodload zsh/zutil
    zparseopts -D -F -K -- \
        {h,-help}=help \
        -charset:=charset \
        -collate:=collate \
        || return 1

    if [[ $# -eq 0 ]] || [[ -n "${help}" ]]; then
        echo -n "Create new user and database with the same name

Usage:

  ${progname} <user> [<password>]

Options:

  --charset  Database character set [Default: utf8]
  --collate  Database collation [Default: utf8_general_ci]

Example:

  ${progname} app

  ${progname} --charset=latin1 --collate=latin1_swedish_ci bob@%
"
        return 0
    fi

    mysql-create-user "$@" \
        && mysql-create-database \
            "${charset[1]:+--charset=}${charset[2]#=}" \
            "${collate[1]:+--collate=}${collate[2]#=}" \
            "${1%@*}" "${1}"
}

mysql-create-user-and-database "$@"
